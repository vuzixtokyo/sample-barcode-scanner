apply plugin: 'com.android.application'

android {
    compileSdkVersion 15
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.example.barcodescanner.android"
        minSdkVersion 15
        targetSdkVersion 15
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        // debug {
        //     storeFile file("debug.keystore")
        // }

        release {
            File propFile = rootProject.file("another.properties")
            if (propFile.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propFile))

                storeFile file(props.storeFile)
                storePassword props.storePassword
                keyAlias props.keyAlias
                keyPassword props.keyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "device"
    productFlavors {
        defaultFlavor {
            dimension "device"
        }
        vuzix {
            applicationId 'com.vuzix.janscan.android'
            versionCode 101
            versionName "1.0.1"

            dimension "device"
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.zxing:core:3.3.0'
}
